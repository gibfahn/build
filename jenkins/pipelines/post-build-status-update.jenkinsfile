#!/usr/bin/env groovy

// DESCRIPTION:
// Updates the status of a GitHub pull request via the Github Bot. Primarily
// used for node-test-commit-* sub builds.

import groovy.json.JsonOutput

pipeline {
    agent { label 'jenkins-workspace' }

    parameters {
        string(name: 'REPO', defaultValue: 'node', description: 'GitHub repository')
        string(name: 'IDENTIFIER', defaultValue: '', description: 'test/aix, linter, etc.')
        string(name: 'STATUS, 'defaultValue: '', description: 'pending, success, unstable, failure')
        string(name: 'URL, 'defaultValue: '', description: 'URL for upstream Jenkins job')
        string(name: 'COMMIT, 'defaultValue: '', description: 'Current commit being tested in upstream Jenkins job')
        string(name: 'REF, 'defaultValue: '', description: 'Current branch being tested in upstream Jenkins job')
    }

    stages {
        stage('Send status report') {
            steps {
                validateParams(params)
                sendBuildStatus(params.REPO, params.IDENTIFIER, params.STATUS, params.URL, params.COMMIT, params.REF)
            }
        }
    }
}

def sendBuildStatus(repo, identifier, status, url, commit, ref) {

    // Fail quietly if upstream job shouldn't be posting to GitHub.
    if (ref.contains('refs/pull/') || POST_STATUS_TO_PR != 'true') {
        println "Skipping status update (ref: ${ref}, " +
            "POST_STATUS_TO_PR: ${POST_STATUS_TO_PR})."
        return
    }

    def path = ""
    def message = ""

    if (status == "pending") {
        path = "start"
        message = "running tests"
    } else if (status == "failure") {
        path = "end"
        message = "tests failed"
    } else if (status == "unstable") {
        path = "end"
        message = "flaky tests failed"
        status = "success"
    } else if (status == "success") {
        path = "end"
        message = "tests passed"
    }

    def buildPayload = JsonOutput.toJson([
        'identifier': identifier,
        'status': status,
        'url': url,
        'commit': commit,
        'ref': ref,
        'message': message
        ])

    def script = "curl -s -o /dev/null --connect-timeout 5 -X POST " +
        "-H 'Content-Type: application/json' -d '${buildPayload}' " +
        "http://github-bot.nodejs.org:3333/${repo}/jenkins/${path}"

    sh(returnStdout: true, script: script)
}

def validateParams(params) {
    // Fail loudly if upstream job is configured wrong.
    if (params.IDENTIFIER == '' || params.STATUS == '' || params.URL == '' ||
        params.COMMIT == '' || params.REF == '') {
        error('All parameter fields are required.')
    }
}
